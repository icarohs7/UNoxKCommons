plugins {
	id "com.jfrog.bintray" version "1.8.3"
	id 'org.jetbrains.kotlin.jvm' version '1.2.50'
	id 'com.github.johnrengelman.plugin-shadow' version '2.0.3'
	id 'com.palantir.git-version' version '0.11.0'
	id 'maven-publish'
}

dependencies {
	implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
	testImplementation 'org.slf4j:slf4j-simple:1.7.25'
	testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.1.6'
}

repositories {
	mavenCentral()
}

group 'com.github.icarohs7'
version gitVersion().toString().find('(\\d\\.\\d\\.\\d)')
sourceCompatibility = 1.8
targetCompatibility = 1.8

compileKotlin {
	kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
	kotlinOptions.jvmTarget = '1.8'
}

clean {
	new File('out').deleteDir()
}

test {
	useJUnitPlatform()
}

kotlin {
	experimental {
		coroutines "enable"
	}
}

task wrapper(type: Wrapper) {
	gradleVersion '4.8.1'
}

bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_KEY')
	publications = ['mavenJava']
	
	pkg {
		repo = 'libraries'
		name = 'unoxkcommons'
		licenses = ['MIT']
		vcsUrl = 'https://github.com/icarohs7/UNoxKCommons.git'
		
		version {
			name = project.version
			released = new Date()
			vcsTag = project.version
		}
	}
}

task sourcesJar(type: Jar, dependsOn: project.classes) {
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: project.javadoc) {
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar, javadocJar
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId project.bintray.pkg.name
			from components.java
			
			artifact sourcesJar {
				classifier = 'sources'
			}
			artifact javadocJar {
				classifier = 'javadoc'
			}
		}
	}
}