plugins {
	id "com.jfrog.bintray" version "1.8.3"
	id 'org.jetbrains.kotlin.jvm' version '1.2.50'
	id 'com.github.johnrengelman.plugin-shadow' version '2.0.3'
	id 'com.palantir.git-version' version '0.11.0'
	id 'maven-publish'
}

dependencies {
	implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
	testImplementation 'org.slf4j:slf4j-simple:1.7.25'
	testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.1.6'
}

repositories {
	mavenCentral()
}

group 'com.github.icarohs7'
description 'Biblioteca de componentes e funções de extensão para a linguagem Kotlin'
version gitVersion().toString().find('(\\d\\.\\d\\.\\d)')
sourceCompatibility = 1.8
targetCompatibility = 1.8

compileKotlin {
	kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
	kotlinOptions.jvmTarget = '1.8'
}

clean {
	new File('out').deleteDir()
}

test {
	useJUnitPlatform()
}

kotlin {
	experimental {
		coroutines "enable"
	}
}

task wrapper(type: Wrapper) {
	gradleVersion '4.8.1'
}

def properties = new Properties()
properties.load(new FileInputStream('local.properties'))

bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_KEY')
	publications = ['mavenJava']
	publish = true
	
	pkg {
		repo = 'libraries'
		name = 'unoxkcommons'
		desc = project.description
		licenses = ['MIT']
		websiteUrl = 'https://github.com/icarohs7/UNoxKCommons'
		issueTrackerUrl = 'https://github.com/icarohs7/UNoxKCommons/issues'
		vcsUrl = 'https://github.com/icarohs7/UNoxKCommons.git'
		githubRepo = 'icarohs7/UNoxKCommons'
		publicDownloadNumbers = true
		
		version {
			name = project.version
			released = new Date()
			vcsTag = project.version
			
			gpg {
				sign = true //Determines whether to GPG sign the files. The default is false
				passphrase = properties.getProperty('GPG_PASS') //Optional. The passphrase for GPG signing'
			}
			mavenCentralSync {
				sync = true //[Default: true] Determines whether to sync the version to Maven Central.
				user = properties.getProperty('OSS_USER') //OSS user token: mandatory
				password = properties.getProperty('OSS_PASS') //OSS user password: mandatory
				close = '1'
				//Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
			}
		}
	}
}

task sourcesJar(type: Jar, dependsOn: project.classes) {
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: project.javadoc) {
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar, javadocJar
}

def pomConfig = {
	licenses {
		license {
			name 'MIT License'
			url 'https://opensource.org/licenses/MIT'
		}
	}
	developers {
		developer {
			id 'icarohs7'
			name 'Icaro R D Temponi'
			email 'icarohs7@gmail.com'
		}
	}
	
	scm {
		url "https://github.com/icarohs7/UNoxKCommons.git"
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId project.bintray.pkg.name
			groupId project.group
			version project.version
			from components.java
			
			pom.withXml {
				def root = asNode()
				root.appendNode('description', project.description)
				root.appendNode('name', project.bintray.pkg.name)
				root.appendNode('url', project.bintray.pkg.websiteUrl)
				root.children().last() + pomConfig
			}
			
			artifact sourcesJar {
				classifier = 'sources'
			}
			artifact javadocJar {
				classifier = 'javadoc'
			}
		}
	}
}